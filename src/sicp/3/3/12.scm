(define x (list 'a 'b))

;;          +---+---+      +---+---+
;;   x ---> | * | *-|----> | * | / |
;;          +---+---+      +---+---+
;;            |              |
;;            V              V
;;          +---+          +---+
;;          | a |          | b |
;;          +---+          +---+

(define y (list 'c 'd))

;;          +---+---+      +---+---+
;;   y ---> | * | *-|----> | * | / |
;;          +---+---+      +---+---+
;;            |              |
;;            V              V
;;          +---+          +---+
;;          | c |          | d |
;;          +---+          +---+

(define z (append x y))
z
;; (a b c d)
(cdr x)
;; (b)

;;          +---+---+      +---+---+      +---+---+      +---+---+
;;   z ---> | * | *-|----> | * | *-|----> | * | *-|----> | * | / |
;;          +---+---+      +---+---+      +---+---+      +---+---+
;;            |              |              |              |
;;            V              V              V              V
;;          +---+          +---+          +---+          +---+
;;          | a |          | b |          | c |          | d |
;;          +---+          +---+          +---+          +---+

(define (append! x y)
  (set-cdr! (last-pair x) y)
  x)

(define (last-pair x)
  (if (null? (cdr x))
      x
      (last-pair (cdr x))))

(define w (append! x y))
w
;; (a b c d)
(cdr x)
;; (b c d)

;;              w
;;              |
;;              V
;;          +---+---+      +---+---+
;;   x ---> | * | *-|----> | * | *-|----------------+
;;          +---+---+      +---+---+                |
;;            |              |                      V
;;            V              V                  +---+---+      +---+---+
;;          +---+          +---+         y ---> | * | *-|----> | * | / |
;;          | a |          | b |                +---+---+      +---+---+
;;          +---+          +---+                  |              |
;;                                                V              V
;;                                              +---+          +---+
;;                                              | c |          | d |
;;                                              +---+          +---+
